apply plugin: 'com.android.application'
//kotlin
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-android'
//bugly
apply plugin: 'bugly'
bugly {
    appId = '38552d428b' // 注册时分配的App ID
    appKey = '2b18d6cb-0974-46a6-af29-c68960fe604e' // 注册时分配的App Key
}
//Sophix
apply plugin: 'com.aliyun.ams.emas-services'
android {
    signingConfigs {
        frame {
            keyAlias 'frame'
            keyPassword '123456'
            storeFile file('/Users/chenzhipeng/移动硬盘/android/框架/Frame/frame.jks')
            storePassword '123456'
        }
    }
    compileSdkVersion rootProject.ext.android["compileSdkVersion"]
    buildToolsVersion rootProject.ext.android["buildToolsVersion"]
    defaultConfig {
        applicationId "com.htxtdshopping.htxtd.frame"
        minSdkVersion rootProject.ext.android["minSdkVersion"]
        targetSdkVersion rootProject.ext.android["targetSdkVersion"]
        versionCode rootProject.ext.android["versionCode"]
        versionName rootProject.ext.android["versionName"]
        testInstrumentationRunner rootProject.ext.android["androidJUnitRunner"]
        signingConfig signingConfigs.frame
        ndk {
            abiFilters 'armeabi-v7a', 'x86'
        }
        //方法数超过65536时要加这行
        multiDexEnabled true
        //ARouter，objectbox
        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [AROUTER_MODULE_NAME: project.getName(), 'objectbox.debug': 'false']
            }
        }
        //databinding
        dataBinding.enabled = true
    }
    buildTypes {
        debug {
            //开启混淆会增加编译时间
            minifyEnabled false    //混淆
            shrinkResources false  //资源文件压缩
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.frame
        }
        release {
            minifyEnabled true    //混淆
            shrinkResources true  //资源文件压缩
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.frame
        }
    }
    productFlavors {
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    dexOptions {
        javaMaxHeapSize "4g"
        jumboMode = true
        preDexLibraries = false
        additionalParameters = [
                '--multi-dex',//多分包
                '--set-max-idx-number=60000'//每个包内方法数上限
        ]
    }
    //自定义打包时apk名称
    applicationVariants.all { variant ->
        variant.outputs.all { output ->
            def fileName = "Frame-${variant.versionName}-${variant.buildType.name}.apk"
//            def fileName = "Frame-${variant.productFlavors[0].name}-${variant.versionName}-${variant.buildType.name}.apk"
            def outFile = output.outputFile
            if (outFile != null && outFile.name.endsWith('.apk')) {
                outputFileName = fileName
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'

    //kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    //common
    implementation project(path: ':common')

    //glide
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    annotationProcessor rootProject.ext.dependencies["glide-compiler"]

    //arouter
    annotationProcessor rootProject.ext.dependencies["arouter-compiler"]

    if (!isBuildModule.toBoolean()) {
        //使用runtimeOnly会是databinding失效
        implementation project(":materialdesign")
        implementation project(":web")
    }

    //image_picker
    implementation project(path: ':image_picker')

    //banner
    implementation rootProject.ext.dependencies["banner"]

    //bugly
    implementation 'com.tencent.bugly:crashreport_upgrade:1.4.2'
    implementation 'com.tencent.bugly:nativecrashreport:3.7.47'
    implementation 'com.tencent.tinker:tinker-android-lib:1.9.9'

    //Sophix
    implementation 'com.aliyun.ams:alicloud-android-hotfix:3.2.8'

    //oss
    implementation 'com.aliyun.dpa:oss-android-sdk:2.9.4'

    //GSYVideoPlayer
    implementation rootProject.ext.dependencies["GSYVideoPlayer"]

    //okdownload
    implementation 'com.liulishuo.okdownload:okdownload:1.0.5'
    // provide sqlite to store breakpoints
    implementation 'com.liulishuo.okdownload:sqlite:1.0.5'

    //easyfloat
    implementation project(path: ':easyfloat')

    //websocket
    implementation "org.java-websocket:Java-WebSocket:1.5.1"

    //ObjectBox
    implementation 'com.getkeepsafe.relinker:relinker:1.3.1'
    implementation "io.objectbox:objectbox-rxjava:2.5.1"
    debugImplementation "io.objectbox:objectbox-android-objectbrowser:2.5.1"
    releaseImplementation "io.objectbox:objectbox-android:2.5.1"

    //MarqueeView
    implementation 'com.sunfusheng:MarqueeView:1.4.1'

    //pickerview
    implementation project(path: ':pickerview')

    //PictureSelector
    implementation 'com.github.LuckSiege.PictureSelector:picture_library:v2.6.0'

    //bga-qrcode-zxing
    implementation 'cn.bingoogolapple:bga-qrcode-zxing:1.3.7'

    //CameraView
    implementation 'cjt.library.wheel:camera:1.1.9'
}
apply plugin: 'io.objectbox'
